#!/bin/zsh

### Generate git clone commands for parallel background jobs
#function __antidote_clone_cmd {
  emulate -L zsh; setopt local_options $_adote_funcopts

  local git_url repo_dest branch repo_name background
  local -a git_opts

  repo_name="$1"
  git_url="$2"
  repo_dest="$3"
  branch="$4"
  background="${5:-1}"
  git_opts=(--quiet --recurse-submodules --shallow-submodules)
  if [[ -n "$branch" ]]; then
    git_opts+=(--branch "${(q)branch}")
  fi

  if [[ $git_url != *://* && $git_url != git@*:*/* ]]; then
    # print -ru2 -- "# Bad git URL provided: $git_url"
    return 1
  fi

  # We print the "cloning..." message to stderr so that we can redirect stdout to the
  # static file without the noise.
  print -r -- "print -ru2 -- '# antidote cloning $repo_name...'"
  print -rn -- "git clone $git_opts ${(q)git_url} ${(q)repo_dest}"
  if [[ "$background" -eq 1 ]]; then
    print -rn -- " &"
  fi
  print
#}
