#!/bin/zsh

### List cloned bundles.
#
# usage: antidote list [-h|--help] [-s|--short] [-d|--dirs] [-u|--url]
#
#function antidote-list {
  emulate -L zsh; setopt local_options $_adote_funcopts
  local TAB=$'\t'

  local o_help o_format o_short o_url o_dirs formatstr formatargs=()
  zparseopts $_adote_zparopt_flags -- \
    h=o_help    -help=h    \
    d=o_dirs    -dirs=d    \
    s=o_short   -short=s   \
    u=o_url     -url=u     \
    f:=o_format -format:=f ||
    return 1

  if (( $#o_help )); then
    antidote-help list
    return
  fi

  if (( $#o_dirs )); then
    [[ -z "$formatstr" ]] || formatstr+="$TAB"
    formatstr="%s"
    formatargs+=("%p")
  fi
  if (( $#o_url )); then
    [[ -z "$formatstr" ]] || formatstr+="$TAB"
    formatstr+="%s"
    formatargs+=("%u")
  fi
  if (( $#o_short )); then
    [[ -z "$formatstr" ]] || formatstr+="$TAB"
    formatstr+="%s"
    formatargs+=("%r")
  fi
  if [[ -z "$formatstr" ]]; then
    formatstr='%-64s %s'
    formatargs=('%u' '%p')
  fi

  if (( $#o_format )); then
    antidote2 list -f "$o_format[-1]" "$@"
  else
    if [[ $# -ne 0 ]]; then
      print -ru2 "antidote: error: unexpected $1, try --help"
      return 1
    fi
    antidote2 list -f "$formatstr" "${formatargs[@]}"
  fi
#}
