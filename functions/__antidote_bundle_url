#!/bin/zsh

### Get the url from a repo bundle.
#function __antidote_bundle_url {
  emulate -L zsh; setopt local_options $_adote_funcopts
  local MATCH MBEGIN MEND; local -a match mbegin mend  # appease 'warn_create_global'
  local gitsite bundle bundle_type

  bundle="$1"
  bundle_type="$(__antidote_bundle_type $bundle)"

  if [[ "$bundle_type" == (url|sshurl) ]]; then
    print -r -- $bundle
  elif [[ "$bundle_type" == repo ]]; then
    # Allow the user to define a preferred git site in case they primarily use something
    # that isn't GitHub.
    zstyle -s ':antidote:gitremote' url 'gitsite' \
      || gitsite='https://github.com'
    gitsite="${gitsite%/}"
    print -r -- ${gitsite}/${bundle}
  else
    return 1
  fi
#}
