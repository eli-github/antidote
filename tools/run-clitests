#!/usr/bin/env zsh
0=${(%):-%x}
setopt extended_glob

local PRJDIR="${0:A:h:h}"
cd "$PRJDIR"

local o_unit o_rev
zparseopts -D -M -- -unit=o_unit -rev=o_rev || return 1

testfiles=()
if (( $# > 0 )); then
  testfiles=($@)
elif (( $#o_unit )); then
  testfiles=($PRJDIR/tests/README.md $PRJDIR/tests/test_*.md~*test_real*~*foo*)
else
  testfiles=($PRJDIR/tests/README.md $PRJDIR/tests/test_*.md)
fi

# if tests are run in reverse order, I can catch places where I didn't teardown properly
if (( $#o_rev )); then
  testfiles=(${(O)testfiles})
fi

# foo example test command
# env -i PATH=$PATH FPATH=$FPATH \
#  zsh -f -- =clitest --list-run --progress dot --prompt '%' --color always $PRJDIR/tests/foo.md

# Use ZSH_BINARY if set, otherwise fallback to default zsh
ZSH=${ZSH_BINARY:-zsh}

env -i PATH=$PATH FPATH=$FPATH PAGER=cat \
    $ZSH -f -- \
    =clitest \
        --list-run --progress dot --prompt '%' \
        --color always \
        --pre-flight 'git --version; print $PRJDIR $VENDOR $OSTYPE =$ZSH $ZSH_VERSION $ZSH_PATCHLEVEL' \
        -- $testfiles
